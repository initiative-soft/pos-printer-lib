buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.9.17"
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.2.51'
    id 'org.jetbrains.dokka' version '0.9.17'
    id 'java-library'
    id 'maven'
    id 'signing'
}

group 'com.initiative-soft'
version '0.1.0'

archivesBaseName = "pos-printer-lib"

repositories {
    mavenCentral()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

jar {
    manifest {
        attributes('Implementation-Title': project.name,
                'Implementation-Version': project.version)
    }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'pos-printer-lib'
                packaging 'jar'
                // optionally artifactId can be defined here
                description 'A library aimed to unify and simplify working with PoS printers'
                url 'https://github.com/initiative-soft/pos-printer-lib'

                scm {
                    connection 'scm:git:git@github.com:initiative-soft/pos-printer-lib.git'
                    developerConnection 'scm:git:git@github.com:initiative-soft/pos-printer-lib.git'
                    url 'https://github.com/initiative-soft/pos-printer-lib'
                }

                licenses {
                    license {
                        name 'Apache License, Version 2.0'
                        url 'https://www.apache.org/licenses/LICENSE-2.0'
                    }
                }

                developers {
                    developer {
                        id 'initiative.soft.com'
                        name 'Initiative soft'
                        email 'office@initiative-soft.com'
                    }
                }
            }
        }
    }
}